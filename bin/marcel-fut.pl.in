#!@PERL@

use warnings;
use strict;
use Math::BigInt;	# 64bit time stamp processing on 32bit archs
use Getopt::Std;

sub usage {
	print "marcel-fut.pl [options] <prof_file>\n";
	print "\t-h \t\tdisplay usage info\n";
	print "\t-q \t\tquiet mode\n";
	print "\t-o [text|paje] \toutput mode\n";
	print "\f-f [papi,special,...] \tfilter out some event subsets (comma separated list)\n";
	print "\t-t \t\ttime warp mode\n";
	exit 0;
}

# Dispatch hash tables
my %code_hash;
my %code_name_hash;
my %handler_hash;

# State hash tables
my %thread_hash;

# Thread <-> VP relationship tables
my %vp_th_hash;

# Runqueue levels counter
my $nb_levels = 0;

# Reference timestamp
my $ev_ref_tstamp = undef;


# Settings

## 0 - display Marcel's special threads (idle, run-task, ksoftirqd)
## 1 - do not display Marcel's special threads
my $filter_out_special_threads = 0;

## 0 - display PAPI samples values
## 1 - do not display PAPI samples values
my $filter_out_papi_events = 0;

## 'text' - selects text-mode trace output
## 'paje' - selects paje-mode trace output
my $output_mode = 'text';

## 0 - display unknown/unhandled events
## 1 - do not display unknown/unhandled events
my $quiet_mode = 0;

## 0 - use effective event times as timestamps
## 1 - use a virtual timer as timestamps
my $time_warp_mode = 0;

# Command line processing
my %opts;
my $ret	= getopts('hqo:f:t', \%opts);	# -h -q -o <output_mode> -f <filtered_out_event_subsets -t

if (exists $opts{'h'}) {
	usage();
}

if (exists $opts{'q'}) {
	$quiet_mode	= 1;
}

if (exists $opts{'o'}) {
	$output_mode	= $opts{'o'};
	if ($output_mode ne 'paje' and $output_mode ne 'text') {
		usage();
	}
}

my %filter_out;
if (exists $opts{'f'}) {
	my $filter_events	= $opts{'f'};
	my @filter_event_list = split /,/, $filter_events;
	foreach (@filter_event_list) {
		$filter_out{$_} = 1;
	}
}

if (exists $filter_out{'special'}) {
	$filter_out_special_threads = 1;
}

if (exists $filter_out{'papi'}) {
	$filter_out_papi_events = 1;
}

if (exists $opts{'t'}) {
	$time_warp_mode	= 1;
}

# Read FUT codes / code_names mapping
open my $code_desc, "fut-codes.pl|" or die "open [fut-codes.pl|] failed: $!\n";

while (<$code_desc>) {
	chomp;
	my ($code_name, $code_val) = split /,/;
	#print "$code_name: $code_val\n";
	${code_hash}{$code_name} = $code_val;
	if (! exists ${code_name_hash}{$code_val}) {
		${code_name_hash}{$code_val} = $code_name;
	}
}

# Helpers
sub suffix_from_name($) {
	my $name = shift;
	my $suffix = '';
	if ($name =~ /^idle/) {
		$suffix = "_i";
	} elsif ($name =~ /^ksoftirqd/) {
		$suffix = "_k";
	} elsif ($name =~ /^run_task/) {
		$suffix = "_r";
	}
	return $suffix;
}

sub display_arrow($$) {
	my $thread_from = shift;
	my $thread_to = shift;

	unless (exists ${$thread_from}{'created'}) {
		return 0;
	}

	unless (exists ${$thread_to}{'created'}) {
		return 0;
	}

	if ($filter_out_special_threads == 1 and (${$thread_from}{'suffix'} ne '' or ${$thread_to}{'suffix'} ne '')) {
		return 0;
	}

	return 1;
}

# Generate time stamps out of event numbers or event times
sub tstamp($) {
	my $event = shift;
	my $ev_num = ${$event}{'num'};
	my $ev_time = ${$event}{'time'};
	my $result;

	if ($time_warp_mode) {
		unless (defined $ev_ref_tstamp) {
			$ev_ref_tstamp = $ev_num;
		}
		$result = $ev_num - $ev_ref_tstamp;
	} else {
		unless (defined $ev_ref_tstamp) {
			$ev_ref_tstamp = $ev_time;
		}
		$result = $ev_time - $ev_ref_tstamp;
	}

	return $result;
}

sub tstamp_minus_e($) {
	my $event = shift;
	my $result = tstamp($event);
	if ($time_warp_mode) {
		$result = $result - 0.1;
	} else {
		$result = $result - 100000;
	}
	return $result;
}

sub tstamp_plus_e($) {
	my $event = shift;
	my $result = tstamp($event);
	if ($time_warp_mode) {
		$result = $result + 0.1;
	} else {
		$result = $result + 100000;
	}
	return $result;
}

# - Paje output mode -
sub handle_paje_FUT_SETUP_CODE		($) {
	print <<END_PAJE_HEADER
%EventDef	PajeDefineContainerType	1
%	Alias	string
%	ContainerType	string
%	Name	string
%EndEventDef
%EventDef	PajeDefineEventType	2
%	Alias	string
%	ContainerType	string
%	Name	string
%EndEventDef
%EventDef	PajeDefineStateType	3
%	Alias	string
%	ContainerType	string
%	Name	string
%EndEventDef
%EventDef	PajeDefineVariableType	4
%	Alias	string
%	ContainerType	string
%	Name	string
%EndEventDef
%EventDef	PajeDefineLinkType	5
%	Alias	string
%	ContainerType	string
%	SourceContainerType	string
%	DestContainerType	string
%	Name	string
%EndEventDef
%EventDef	PajeDefineEntityValue	6
%	Alias	string
%	EntityType	string
%	Name	string
%	Color	color
%EndEventDef
%EventDef	PajeCreateContainer	7
%	Time	date
%	Alias	string
%	Type	string
%	Container	string
%	Name	string
%EndEventDef
%EventDef	PajeDestroyContainer	8
%	Time	date
%	Name	string
%	Type	string
%EndEventDef
%EventDef	PajeNewEvent	9
%	Time	date
%	Type	string
%	Container	string
%	Value	string
%EndEventDef
%EventDef	PajeSetState	10
%	Time	date
%	Type	string
%	Container	string
%	Value	string
%       ThreadName      string
%EndEventDef
%EventDef	PajePushState	11
%	Time	date
%	Type	string
%	Container	string
%	Value	string
%EndEventDef
%EventDef	PajePushState	111
%	Time	date
%	Type	string
%	Container	string
%	Value	string
%	Object	string
%EndEventDef
%EventDef	PajePopState	12
%	Time	date
%	Type	string
%	Container	string
%EndEventDef
%EventDef	PajeSetVariable	13
%	Time	date
%	Type	string
%	Container	string
%	Value	double
%EndEventDef
%EventDef	PajeAddVariable	14
%	Time	date
%	Type	string
%	Container	string
%	Value	double
%EndEventDef
%EventDef	PajeSubVariable	15
%	Time	date
%	Type	string
%	Container	string
%	Value	double
%EndEventDef
%EventDef	PajeStartLink	16
%	Time	date
%	Type	string
%	Container	string
%	Value	string
%	SourceContainer	string
%	Key	string
%	Size	int
%EndEventDef
%EventDef	PajeEndLink	17
%	Time	date
%	Type	string
%	Container	string
%	Value	string
%	DestContainer	string
%	Key	string
%	Size	int
%EndEventDef
%EventDef	PajeStartLink	18
%	Time	date
%	Type	string
%	Container	string
%	Value	string
%	SourceContainer	string
%	Key	string
%EndEventDef
%EventDef	PajeEndLink	19
%	Time	date
%	Type	string
%	Container	string
%	Value	string
%	DestContainer	string
%	Key	string
%EndEventDef
%EventDef	PajeNewEvent   20
%	Time	date
%	Type	string
%	Container	string
%	Value	string
%       EventName      string
%EndEventDef
%EventDef	PajeNewEvent   21
%	Time	date
%	Type	string
%	Container	string
%	Value	string
%       EventName      string
%       ThreadName      string
%EndEventDef
%EventDef	PajeNewEvent   112
%	Time	date
%	Type	string
%	Container	string
%	Value	string
%       ThreadName      string
%       ThreadGroup     string
%       ThreadParent    string
%EndEventDef
1	CT_D	0	Display_type
1	CT_M	CT_D	Machine_container_type
1	CT_Mvp	CT_D	Machine_container_type_vp
1	CT_VPvp	CT_Mvp	VP_container_type_vp
1	CT_T	CT_M	Thread_container_type
1	CT_TE	CT_T	Thread_event_container_type
3	ST_BT	CT_TE	Barrier_thread_state_type
2	ET_BT	CT_TE	Barrier_thread_event_type
3	ST_T	CT_T	Thread_state_type
3	ST_VPvp	CT_VPvp	VP_state_type_vp
6	bw	ST_BT	waiting	\"0.0 0.0 0.9\"
6	br	ST_BT	running	\"0.5 0.5 0.5\"
6	s	ST_T	sleep	\"0.5 0.2 0.2\"
6	s_u	ST_T	sleep	\"0.5 0.5 0.5\"
6	s_i	ST_T	sleep	\"0.4 0.4 0.0\"
6	s_k	ST_T	sleep	\"0.5 0.2 0.0\"
6	s_r	ST_T	sleep	\"0.0 0.3 0.5\"
6	r	ST_T	running	\"0.0 1.0 0.0\"
6	r_u	ST_T	running	\"0.8 0.8 0.8\"
6	r_i	ST_T	running	\"0.9 0.9 0.0\"
6	r_k	ST_T	running	\"0.9 0.6 0.0\"
6	r_r	ST_T	running	\"0.0 0.6 0.9\"
6	d	ST_T	dead	\"0.1 0.1 0.1\"
6	vp	ST_VPvp	user	\"0.0 1.0 0.5\"
6	vp_u	ST_VPvp	undef	\"0.8 0.8 0.8\"
6	vp_i	ST_VPvp	idle	\"0.9 0.9 0.5\"
6	vp_k	ST_VPvp	ksoftirqd	\"0.9 0.6 0.5\"
6	vp_r	ST_VPvp	run_task	\"0.0 0.5 0.9\"
7	0	D	CT_D	0	Display
7	0	M	CT_M	D	Machine
7	0	Mvp	CT_Mvp	D	VPs
5	LT_SW	CT_M	CT_T	CT_T	Link_type_switch
7	0	VPvp0	CT_VPvp	Mvp	VP_0
4       VAR_PAPI_0      CT_T    \"Papi event 0\"
4       VAR_PAPI_1      CT_T    \"Papi event 1\"
4       VAR_PAPI_2      CT_T    \"Papi event 2\"
4       VAR_PAPI_3      CT_T    \"Papi event 3\"
4       VAR_PAPI_0vp    CT_VPvp    \"Papi event 0\"
4       VAR_PAPI_1vp    CT_VPvp    \"Papi event 1\"
4       VAR_PAPI_2vp    CT_VPvp    \"Papi event 2\"
4       VAR_PAPI_3vp    CT_VPvp    \"Papi event 3\"
END_PAJE_HEADER
}

sub paje_display_create_thread($$) {
	my $event = shift;
	my $thread = shift;
	my $ev_tstamp = tstamp($event);
	my $th_tid = ${$thread}{'tid'};
	unless (exists ${$thread}{'created'}) {
		print "7 $ev_tstamp T$th_tid CT_T M Thread_$th_tid\n";
		${$thread}{'created'} = 1;
	}
}

sub paje_display_create_vp($$) {
	my $event = shift;
	my $vp_num = shift;
	my $ev_tstamp = tstamp($event);
	print "7 $ev_tstamp VPvp$vp_num CT_VPvp Mvp VP_$vp_num\n";
}

sub paje_display_thread_state($$) {
	my $event = shift;
	my $thread = shift;
	my $ev_tstamp = tstamp($event);
	if (exists ${$thread}{'created'}) {
		unless ($filter_out_special_threads and ${$thread}{'suffix'} ne '') {
			print "10 $ev_tstamp ST_T T${$thread}{'tid'} ${$thread}{'state'}${$thread}{'suffix'} \"${$thread}{'name'}\"\n";
		}
	}
}

sub paje_display_vp_thread_state($$) {
	my $event = shift;
	my $thread = shift;
	my $ev_tstamp = tstamp($event);
	my $vp_num = 0;
	if (exists ${$thread}{'vp'}) {
		$vp_num = ${$thread}{'vp'};
	}
	print "10 $ev_tstamp ST_VPvp VPvp$vp_num vp${$thread}{'suffix'} \"VP_$vp_num\"\n";
}

sub paje_display_switch_arrow_begin($$) {
	my $event = shift;
	my $thread_from = shift;
	my $b_ev_tstamp = tstamp_minus_e($event);
	print "18 $b_ev_tstamp LT_SW M 0 T${$thread_from}{'tid'} ${$event}{'num'}\n";
}

sub paje_display_switch_arrow_end($$) {
	my $event = shift;
	my $thread_to = shift;
	my $e_ev_tstamp = tstamp_plus_e($event);
	print "19 $e_ev_tstamp LT_SW M 0 T${$thread_to}{'tid'} ${$event}{'num'}\n";
}

sub paje_check_gomp_event_container($$) {
	my $event = shift;
	my $thread = shift;

	unless (exists ${$thread}{'gomp_ev'}) {
		${$thread}{'gomp_ev'}= 1;
		my $ev_tstamp = tstamp($event);
		print "7 $ev_tstamp TE${$thread}{'tid'} CT_TE T${$thread}{'tid'} \"GOMP events\"\n";
	}
}

# -

sub handle_paje_FUT_THREAD_BIRTH_CODE	($) {
	my $event = shift;
	my $ev_num = ${$event}{'num'};
	my $ev_time = ${$event}{'time'};
	my $ev_tid = ${$event}{'tid'};
	my $param_array = ${$event}{'params'};
	my $th_tid = shift @$param_array;
	my ($param_1) = unpack 'l', (pack 'q', (@$param_array));

	my $thread;
	if (exists ${thread_hash}{$th_tid}) {
		$thread = ${thread_hash}{$th_tid};
	} else {
		$thread = {};
		${$thread}{'tid'} = $th_tid;
		${thread_hash}{$th_tid} = $thread;
	}
	${$thread}{'name'} = 'undefined';
	${$thread}{'state'} = 's';
	${$thread}{'suffix'} = '_u';

	unless ($filter_out_special_threads) {
		paje_display_create_thread($event, $thread);
		paje_display_thread_state($event, $thread);
	}
}

sub handle_paje_FUT_SET_THREAD_NAME_CODE	($) {
	my $event = shift;
	my $ev_tid = ${$event}{'tid'};
	my $param_array = ${$event}{'params'};
	my $th_tid = shift @$param_array;
	my ($th_name) = unpack 'Z*', (pack 'q*', (@$param_array));
	my $thread = ${thread_hash}{$th_tid};
	${$thread}{'name'} = $th_name;
	${$thread}{'suffix'} = suffix_from_name($th_name);
	if (!$filter_out_special_threads or ${$thread}{'suffix'} eq '') {
		paje_display_create_thread($event, $thread);
		paje_display_thread_state($event, $thread);
		paje_display_vp_thread_state($event, $thread);
	}
}

sub handle_paje_FUT_SWITCH_TO_CODE		($) {
	my $event = shift;
	my $th_from_tid = ${$event}{'tid'};
	my $param_array = ${$event}{'params'};
	my $th_to_tid = ${$param_array}[0];
	
	my $thread_from = ${thread_hash}{$th_from_tid};
	if (${$thread_from}{'state'} ne 'd') {
		${$thread_from}{'state'} = 's';
	}

	my $thread_to = ${thread_hash}{$th_to_tid};
	${$thread_to}{'state'} = 'r';

	my $display_arrow = display_arrow($thread_from, $thread_to);
	if ($display_arrow) {
		paje_display_switch_arrow_begin($event, $thread_from);
	}
	paje_display_thread_state($event, $thread_from);
	paje_display_thread_state($event, $thread_to);

	my $vp_num = 0;
	if (exists ${$thread_from}{'vp'}) {
		$vp_num = ${$thread_from}{'vp'};
		delete ${$thread_from}{'vp'};
	}
	${vp_th_hash}{$vp_num} = $thread_to;
	${$thread_to}{'vp'} = $vp_num;

	paje_display_vp_thread_state($event, $thread_to);
	if ($display_arrow) {
		paje_display_switch_arrow_end($event, $thread_to);
	}
}

sub handle_paje_FUT_THREAD_DEATH_CODE	($) {
	my $event = shift;
	my $param_array = ${$event}{'params'};
	my $th_tid = ${$param_array}[0];
	my $thread = ${thread_hash}{$th_tid};
	${$thread}{'suffix'} = '';
	${$thread}{'state'} = 'd';
	paje_display_thread_state($event, $thread);
}

sub handle_paje_FUT_NEW_LWP_CODE		($) {
	my $event = shift;
	my $param_array = ${$event}{'params'};
	my $vp_num = ${$param_array}[0];
	my $th_tid = ${$param_array}[1];

	my $thread = ${thread_hash}{$th_tid};
	${$thread}{'state'} = 'r';
	${$thread}{'vp'} = $vp_num;
	${vp_th_hash}{$vp_num} = $thread;

	paje_display_thread_state($event, $thread);
	paje_display_create_vp($event, $vp_num);
	paje_display_vp_thread_state($event, $thread);
}

sub handle_paje_FUT_FGOMP_PAPIMAR_EV		($$) {
	my $papi_ev = shift;
	my $event = shift;
	my $ev_tid = ${$event}{'tid'};
	my $param_array = ${$event}{'params'};
	my $ev_val = ${$param_array}[0];
	my $thread = ${thread_hash}{$ev_tid};

	my $vp_num = 0;
	if (exists ${$thread}{'vp'}) {
		$vp_num = ${$thread}{'vp'};
	}

	my $ev_tstamp = tstamp($event);
	print "13 $ev_tstamp VAR_PAPI_$papi_ev T$ev_tid $ev_val\n";
	print "13 $ev_tstamp VAR_PAPI_${papi_ev}vp VPvp$vp_num $ev_val\n";
}

sub handle_paje_FUT_FGOMP_PAPIMAR_EV0		($) {
	my $event = shift;
	handle_paje_FUT_FGOMP_PAPIMAR_EV (0, $event);
}

sub handle_paje_FUT_FGOMP_PAPIMAR_EV1		($) {
	my $event = shift;
	handle_paje_FUT_FGOMP_PAPIMAR_EV (1, $event);
}

sub handle_paje_FUT_FGOMP_PAPIMAR_EV2		($) {
	my $event = shift;
	handle_paje_FUT_FGOMP_PAPIMAR_EV (2, $event);
}

sub handle_paje_FUT_FGOMP_PAPIMAR_EV3		($) {
	my $event = shift;
	handle_paje_FUT_FGOMP_PAPIMAR_EV (3, $event);
}

sub handle_paje_FUT_FGOMP_GOMP_BAR_ENTER	($) {
	my $event = shift;
	my $ev_tid = ${$event}{'tid'};
	my $thread = ${thread_hash}{$ev_tid};
	paje_check_gomp_event_container($event, $thread);
	my $ev_tstamp = tstamp($event);
	print "10 $ev_tstamp ST_BT TE$ev_tid bw \"barrier wait\"\n";
	print "20 $ev_tstamp ET_BT TE$ev_tid \"entering barrier\" \"barrier enter\"\n";
}

sub handle_paje_FUT_FGOMP_GOMP_BAR_LEAVE	($) {
	my $event = shift;
	my $ev_tid = ${$event}{'tid'};
	my $thread = ${thread_hash}{$ev_tid};
	paje_check_gomp_event_container($event, $thread);
	my $ev_tstamp = tstamp($event);
	print "10 $ev_tstamp ST_BT TE$ev_tid br \"running\"\n";
	print "20 $ev_tstamp ET_BT TE$ev_tid \"leaving barrier\" \"barrier leave\"\n";
}

# - Text output mode -
sub handle_FUT_GCC_INSTRUMENT_ENTRY_CODE($) {
	print "<FUT_GCC_INSTRUMENT_ENTRY_CODE>\n";
}

sub handle_FUT_GCC_INSTRUMENT_EXIT_CODE($) {
	print "<FUT_GCC_INSTRUMENT_EXIT_CODE>\n";
}

sub handle_FUT_SETUP_CODE		($) {
	print "<FUT_SETUP_CODE>\n";
}

sub handle_FUT_CALIBRATE0_CODE		($) {
	print "<FUT_CALIBRATE0_CODE>\n";
}

sub handle_FUT_CALIBRATE1_CODE		($) {
	print "<FUT_CALIBRATE1_CODE>\n";
}

sub handle_FUT_CALIBRATE2_CODE		($) {
	print "<FUT_CALIBRATE2_CODE>\n";
}

sub handle_FUT_RQS_NEWLEVEL		($) {
	my $event = shift;
	my $ev_num = ${$event}{'num'};
	my $ev_tid = ${$event}{'tid'};
	my $param_array = ${$event}{'params'};
	my $param_0 = ${$param_array}[0];
	print "$ev_num [$ev_tid]: New level $nb_levels created with [$param_0] runqueues\n";
	$nb_levels++;
}

sub handle_FUT_RQS_NEWRQ		($) {
	my $event = shift;
	my $ev_num = ${$event}{'num'};
	my $ev_tid = ${$event}{'tid'};
	my $param_array = ${$event}{'params'};
	my ($s_temp) = pack 'q', (@$param_array);
	my ($param_0) = unpack 'l', ($s_temp);
	my $param_1 = @$param_array[1];
	print "$ev_num [$ev_tid]: New rq [$param_1] at level [$param_0] created\n";
}

sub handle_BUBBLE_SCHED_NEW		($) {
	my $event = shift;
	my $ev_num = ${$event}{'num'};
	my $ev_tid = ${$event}{'tid'};
	my $param_array = ${$event}{'params'};
	my $param_0 = ${$param_array}[0];
	print "$ev_num [$ev_tid]: New bubble [$param_0] created\n";
}

sub handle_FUT_THREAD_BIRTH_CODE	($) {
	my $event = shift;
	my $ev_num = ${$event}{'num'};
	my $ev_tid = ${$event}{'tid'};
	my $param_array = ${$event}{'params'};
	my $param_0 = ${$param_array}[0];
	shift @$param_array;
	my ($s_temp) = pack 'q', (@$param_array);
	my ($param_1) = unpack 'l', ($s_temp);
	print "$ev_num [$ev_tid]: Thread [$param_0] created on VP [$param_1]\n";
}

sub handle_SET_THREAD_NUMBER		($) {
	my $event = shift;
	my $ev_num = ${$event}{'num'};
	my $ev_tid = ${$event}{'tid'};
	my $param_array = ${$event}{'params'};
	my $param_0 = ${$param_array}[0];
	my $param_1 = ${$param_array}[1];
	print "$ev_num [$ev_tid]: Thread [$param_0] numbered [$param_1]\n";
}

sub handle_FUT_SET_THREAD_NAME_CODE	($) {
	my $event = shift;
	my $ev_num = ${$event}{'num'};
	my $ev_tid = ${$event}{'tid'};
	my $line = ${$event}{'line'};
	my $param_array = ${$event}{'params'};
	my $param_0 = shift @$param_array;
	my ($name_s_temp) = pack 'q*', (@$param_array);
	my ($name_s) = unpack 'Z*', ($name_s_temp);
	my $name = join ' - ',(@$param_array);

	print "$ev_num [$ev_tid]: Thread [$param_0] named [$name_s]\n";
}

sub handle_FUT_SWITCH_TO_CODE		($) {
	my $event = shift;
	my $ev_num = ${$event}{'num'};
	my $ev_tid = ${$event}{'tid'};
	my $param_array = ${$event}{'params'};
	my $param_0 = ${$param_array}[0];
	print "$ev_num [$ev_tid]: Switch to thread $param_0\n";
}

sub handle_FUT_THREAD_DEATH_CODE	($) {
	my $event = shift;
	my $ev_num = ${$event}{'num'};
	my $ev_tid = ${$event}{'tid'};
	my $param_array = ${$event}{'params'};
	my $param_0 = ${$param_array}[0];
	print "$ev_num [$ev_tid]: Thread [$param_0] dead\n";
}

sub handle_FUT_RQS_NEWLWPRQ		($) {
	my $event = shift;
	my $ev_num = ${$event}{'num'};
	my $ev_tid = ${$event}{'tid'};
	my $param_array = ${$event}{'params'};
	my ($s_temp) = pack 'q', (@$param_array);
	my ($param_0) = unpack 'l', ($s_temp);
	my $param_1 = @$param_array[1];
	print "$ev_num [$ev_tid]: New VP rq [$param_1] at VP [$param_0] created\n";
}

sub handle_FUT_NEW_LWP_CODE		($) {
	my $event = shift;
	my $ev_num = ${$event}{'num'};
	my $ev_tid = ${$event}{'tid'};
	my $param_array = ${$event}{'params'};
	my $param_0 = ${$param_array}[0];
	my $param_1 = ${$param_array}[1];
	print "$ev_num [$ev_tid]: New VP [$param_0] running thread [$param_1]\n";
}

sub handle_FUT_FGOMP_PAPIMAR_EV		($$) {
	my $papi_ev = shift;
	my $event = shift;
	my $ev_num = ${$event}{'num'};
	my $ev_tid = ${$event}{'tid'};
	my $param_array = ${$event}{'params'};
	my $ev_val = ${$param_array}[0];

	print "$ev_num [$ev_tid]: PAPI event$papi_ev sample: $ev_val\n";
}

sub handle_FUT_FGOMP_PAPIMAR_EV0		($) {
	my $event = shift;
	handle_FUT_FGOMP_PAPIMAR_EV (0, $event);
}

sub handle_FUT_FGOMP_PAPIMAR_EV1		($) {
	my $event = shift;
	handle_FUT_FGOMP_PAPIMAR_EV (1, $event);
}

sub handle_FUT_FGOMP_PAPIMAR_EV2		($) {
	my $event = shift;
	handle_FUT_FGOMP_PAPIMAR_EV (2, $event);
}

sub handle_FUT_FGOMP_PAPIMAR_EV3		($) {
	my $event = shift;
	handle_FUT_FGOMP_PAPIMAR_EV (3, $event);
}

# - Output mode dispatch -
if ($output_mode eq 'paje') {
	${handler_hash}{ ${code_hash}{'FUT_SETUP_CODE'} } = \&handle_paje_FUT_SETUP_CODE;
	${handler_hash}{ ${code_hash}{'FUT_THREAD_BIRTH_CODE'} } = \&handle_paje_FUT_THREAD_BIRTH_CODE;
	${handler_hash}{ ${code_hash}{'FUT_SET_THREAD_NAME_CODE'} } = \&handle_paje_FUT_SET_THREAD_NAME_CODE;
	${handler_hash}{ ${code_hash}{'FUT_SWITCH_TO_CODE'} } = \&handle_paje_FUT_SWITCH_TO_CODE;
	${handler_hash}{ ${code_hash}{'FUT_THREAD_DEATH_CODE'} } = \&handle_paje_FUT_THREAD_DEATH_CODE;
	${handler_hash}{ ${code_hash}{'FUT_NEW_LWP_CODE'} } = \&handle_paje_FUT_NEW_LWP_CODE;
	${handler_hash}{ ${code_hash}{'FUT_FGOMP_GOMP_BAR_ENTER'} } = \&handle_paje_FUT_FGOMP_GOMP_BAR_ENTER;
	${handler_hash}{ ${code_hash}{'FUT_FGOMP_GOMP_BAR_LEAVE'} } = \&handle_paje_FUT_FGOMP_GOMP_BAR_LEAVE;

	unless ($filter_out_papi_events) {
		${handler_hash}{ ${code_hash}{'FUT_FGOMP_PAPIMAR_EV0'} } = \&handle_paje_FUT_FGOMP_PAPIMAR_EV0;
		${handler_hash}{ ${code_hash}{'FUT_FGOMP_PAPIMAR_EV1'} } = \&handle_paje_FUT_FGOMP_PAPIMAR_EV1;
		${handler_hash}{ ${code_hash}{'FUT_FGOMP_PAPIMAR_EV2'} } = \&handle_paje_FUT_FGOMP_PAPIMAR_EV2;
		${handler_hash}{ ${code_hash}{'FUT_FGOMP_PAPIMAR_EV3'} } = \&handle_paje_FUT_FGOMP_PAPIMAR_EV3;
	}
} else {
	${handler_hash}{ ${code_hash}{'FUT_GCC_INSTRUMENT_ENTRY_CODE'} } = \&handle_FUT_GCC_INSTRUMENT_ENTRY_CODE;
	${handler_hash}{ ${code_hash}{'FUT_GCC_INSTRUMENT_EXIT_CODE'} } = \&handle_FUT_GCC_INSTRUMENT_EXIT_CODE;
	${handler_hash}{ ${code_hash}{'FUT_SETUP_CODE'} } = \&handle_FUT_SETUP_CODE;
	${handler_hash}{ ${code_hash}{'FUT_CALIBRATE0_CODE'} } = \&handle_FUT_CALIBRATE0_CODE;
	${handler_hash}{ ${code_hash}{'FUT_CALIBRATE1_CODE'} } = \&handle_FUT_CALIBRATE1_CODE;
	${handler_hash}{ ${code_hash}{'FUT_CALIBRATE2_CODE'} } = \&handle_FUT_CALIBRATE2_CODE;
	${handler_hash}{ ${code_hash}{'FUT_RQS_NEWLEVEL'} } = \&handle_FUT_RQS_NEWLEVEL;
	${handler_hash}{ ${code_hash}{'FUT_RQS_NEWRQ'} } = \&handle_FUT_RQS_NEWRQ;
	${handler_hash}{ ${code_hash}{'BUBBLE_SCHED_NEW'} } = \&handle_BUBBLE_SCHED_NEW;
	${handler_hash}{ ${code_hash}{'FUT_THREAD_BIRTH_CODE'} } = \&handle_FUT_THREAD_BIRTH_CODE;
	${handler_hash}{ ${code_hash}{'SET_THREAD_NUMBER'} } = \&handle_SET_THREAD_NUMBER;
	${handler_hash}{ ${code_hash}{'FUT_SET_THREAD_NAME_CODE'} } = \&handle_FUT_SET_THREAD_NAME_CODE;
	${handler_hash}{ ${code_hash}{'FUT_SWITCH_TO_CODE'} } = \&handle_FUT_SWITCH_TO_CODE;
	${handler_hash}{ ${code_hash}{'FUT_THREAD_DEATH_CODE'} } = \&handle_FUT_THREAD_DEATH_CODE;
	${handler_hash}{ ${code_hash}{'FUT_RQS_NEWLWPRQ'} } = \&handle_FUT_RQS_NEWLWPRQ;
	${handler_hash}{ ${code_hash}{'FUT_NEW_LWP_CODE'} } = \&handle_FUT_NEW_LWP_CODE;

	unless ($filter_out_papi_events) {
		${handler_hash}{ ${code_hash}{'FUT_FGOMP_PAPIMAR_EV0'} } = \&handle_FUT_FGOMP_PAPIMAR_EV0;
		${handler_hash}{ ${code_hash}{'FUT_FGOMP_PAPIMAR_EV1'} } = \&handle_FUT_FGOMP_PAPIMAR_EV1;
		${handler_hash}{ ${code_hash}{'FUT_FGOMP_PAPIMAR_EV2'} } = \&handle_FUT_FGOMP_PAPIMAR_EV2;
		${handler_hash}{ ${code_hash}{'FUT_FGOMP_PAPIMAR_EV3'} } = \&handle_FUT_FGOMP_PAPIMAR_EV3;
	}
}

# Read Tracefile
my $trace_filename = shift @ARGV;

## Extract trace using fut-print.pl
open my $trace_desc, "fut-print.pl -s ${trace_filename}|" or die "open [fut-print.pl -s ${trace_filename}|] failed: $!\n";

while (<$trace_desc>) {
	chomp;

	# read a line
	my $line = $_;
	my @params	= split ',';	# split the line into fields
	
	# read the event fields
	my $ev_num		= shift @params;
	my $ev_host_num	= shift @params;
	my $ev_time	= Math::BigInt->new(shift @params);
	my $ev_tid	= shift @params;
	my $ev_code	= shift @params;
	my $ev_nb_params	= shift @params;

	# dispatch the event to the suitable event handler
	if (exists ${handler_hash}{$ev_code}) {
		my $event = {};
		${$event}{'line'}	= $line;
		${$event}{'num'}	= $ev_num;
		${$event}{'time'}	= $ev_time;
		${$event}{'host_num'}	= $ev_host_num;
		${$event}{'tid'}	= $ev_tid;
		${$event}{'code'}	= $ev_code;
		${$event}{'nb_params'}	= $ev_nb_params;
		${$event}{'params'}	= \@params;
		my $handler_sub = ${handler_hash}{$ev_code};
		&{$handler_sub}($event);
	} else {
		if ($quiet_mode == 0) {
			if (exists ${code_name_hash}{$ev_code}) {
				print "unhandled event $ev_num: $ev_code - ".(${code_name_hash}{$ev_code})."\n";
			} else {
				print "unhandled event $ev_num: $ev_code - <UNKNOWN CODE>\n";
			}
		}
	}
}


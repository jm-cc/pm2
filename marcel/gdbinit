set $marcel_saved=0

define save-ctx
  if $marcel_saved==1
    printf "already saved ctx\n"
  else
    __save-ctx
    set $marcel_saved=1
  end
end
document save-ctx
Saves the current marcel context
Use rest-ctx to restore it
end

define set-ctx
  if $marcel_saved==0
    save-ctx
  end
  printf "switching to $arg0(%p)\n", $arg0
  __set-ctx $arg0
end
document set-ctx
Switch to another marcel thread's context
end

define rest-ctx
  if $marcel_saved==0
    echo no context saved !\n
  else
    __rest-ctx
  end
end
document rest-ctx
Restores a previously-saved marcel context,
so as to be able to continue execution
end

define marcel-continue
  handle SIGALRM pass
  handle SIGVTALRM pass
  if $marcel_saved==1
    __rest-ctx
  end
end

define marcel-step
  handle SIGALRM nopass
  handle SIGVTALRM nopass
  if $marcel_saved==1
    __rest-ctx
  end
end

define hook-stop
  marcel-step
end

define hook-run
  marcel-continue
end
define hook-continue
  marcel-continue
end

define hook-step
  marcel-step
end
define hook-stepi
  marcel-step
end
define hook-next
  marcel-step
end
define hook-nexti
  marcel-step
end

define hook-finish
  marcel-continue
end
define hook-until
  marcel-continue
end
define hook-advance
  marcel-continue
end

define ma-printrq
  if $arg0
    printf " %s", $arg0->name
  else
    printf " nil"
  end
end

define marcel-printthread
  set $state='?'
  if $arg0->sched.state == 0
    set $state='R'
  end
  if $arg0->sched.state == 1
    set $state='I'
  end
  if $arg0->sched.state == 2
    set $state='U'
  end
  if $arg0->sched.state == 4
    set $state='S'
  end
  if $arg0->sched.state == 8
    set $state='Z'
  end
  if $arg0->sched.state == 16
    set $state='D'
  end
  if $arg0->sched.state == 32
    set $state='G'
  end
  if $arg0->sched.state == 64
    set $state='M'
  end
  if $arg0->sched.state == 128
    set $state='F'
  end
  printf "%p %16s %2d %c %2d", \
    $arg0, $arg0->name, $arg0->sched.internal.prio, $state, \
    *(unsigned *)((char*)$arg0->sched.lwp+((char*)&ma_per_lwp__number-(char*)&__main_lwp))
  ma-printrq $arg0->sched.internal.init_rq
  ma-printrq $arg0->sched.internal.cur_rq
  printf "\n"
end
document marcel-printthread
Prints the state of a thread
end

define marcel-threads
  set $threade=all_threads->next
  while $threade!=&all_threads
    set $thread=(marcel_t)(((char*)$threade)-(unsigned)(&(((marcel_t)0)->all_threads)))
    marcel-printthread $thread
    set $threade=$threade->next
  end
end

document marcel-threads
Prints a list of marcel threads
end
